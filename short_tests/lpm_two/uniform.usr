c Maximum number of local particles
#define LPM_LPART 300 /* make it large enough to account for load imbalance */

c Number of particle equations being solved
#define LPM_LRS 6

#define LPM_JVX (LPM_JZ+1)
#define LPM_JVY (LPM_JZ+2)
#define LPM_JVZ (LPM_JZ+3)

c Number of properties for a particle
#define LPM_LRP 10

#define LPM_R_JRHOP 1
#define LPM_R_JDP   2
#define LPM_R_JVOLP 3
#define LPM_R_JUX   4
#define LPM_R_JUY   5
#define LPM_R_JUZ   6
#define LPM_R_JDPDX 7
#define LPM_R_JDPDY 8
#define LPM_R_JDPDZ 9
#define LPM_R_JRHO  10

C Number of properties to project
#define LPM_LRP_PRO 4

#define LPM_P_JPHIP 1   /* void fraction */
#define LPM_P_JFFX  2
#define LPM_P_JFFY  3
#define LPM_P_JFFZ  4

#include "experimental/lpm.h"

c-----------------------------------------------------------------------
      subroutine uservp (ix,iy,iz,eg)
      include 'SIZE'
      include 'TOTAL'
      include 'NEKUSE'
      integer e,eg

      e = gllel(eg)

      udiff=0.0
      utrans=0.

      return
      end
c-----------------------------------------------------------------------
      subroutine userqtl 
      include 'SIZE'
      include 'TOTAL'
#     include "LPM"

      call lpm_qtl_pvol(usrdiv,lpm_pro_fld(1,1,1,1,LPM_P_JPHIP))

      return
      end
c-----------------------------------------------------------------------
      subroutine userf  (ix,iy,iz,eg)
      include 'SIZE'
      include 'TOTAL'
      include 'NEKUSE'
#     include "LPM"

      integer e,eg

      e = gllel(eg)

      ffx = lpm_pro_fld(ix,iy,iz,e,LPM_P_JFFX)
      ffy = lpm_pro_fld(ix,iy,iz,e,LPM_P_JFFY)
      ffz = lpm_pro_fld(ix,iy,iz,e,LPM_P_JFFZ)
      
      rdum = 1/utrans
      rdum = rdum/(1-lpm_pro_fld(ix,iy,iz,e,LPM_P_JPHIP))

      ffx = ffx*rdum
      ffy = ffy*rdum
      ffz = ffz*rdum

      return
      end
c-----------------------------------------------------------------------
      subroutine userq  (ix,iy,iz,eg)
      include 'SIZE'
      include 'TOTAL'
      include 'NEKUSE'
      integer e,eg

      qvol   = 0.0

      return
      end
c-----------------------------------------------------------------------
      subroutine userchk
      include 'SIZE'
      include 'TOTAL'
#     include "LPM"

      real  p_lagrange(LPM_LRP_PRO)
     >     ,p_eulerian(LPM_LRP_PRO)
     >     ,p_error   (LPM_LRP_PRO)

      real p_error_max
      save p_error_max 
      data p_error_max /-1/

      ! lagrangian properties
      jst = 4
      call rzero(p_lagrange(1),LPM_LRP_PRO)
      do j=1,LPM_LRP_PRO-1
         do i=1,lpm_npart
            p_lagrange(j) = p_lagrange(j) + lpm_cp_map(jst+j,i)
         enddo
         p_lagrange(j) = glsum(p_lagrange(j),1)
      enddo

      ! eulerian volume
      ndum = lx1*ly1*lz1*nelt
      do j=1,LPM_LRP_PRO-1
         p_eulerian(j) = glsc2(bm1,lpm_pro_fld(1,1,1,1,j),ndum)
      enddo

      ! compute relative projection error
      do j=1,LPM_LRP_PRO-1 ! we don't need fz in 2d
         p_error(j) = abs(p_lagrange(j) - p_eulerian(j))
         if (p_error(j) .gt. p_error_max) p_error_max = p_error(j)
      enddo

      if (nid.eq.0 .and. istep.eq.nsteps) write(6,100) p_error_max 
 100  format('proj error: ', E14.7)

      call lpm_solve(time)

      if(mod(istep,iostep).eq.0) then
         call lpm_io_write('',0)
         call outpost2(lpm_pro_fld(1,1,1,1,LPM_P_JFFX)
     >                ,lpm_pro_fld(1,1,1,1,LPM_P_JFFY)
     >                ,lpm_pro_fld(1,1,1,1,LPM_P_JFFZ)
     >                ,lpm_pro_fld(1,1,1,1,LPM_P_JPHIP)
     >                ,lpm_pro_fld(1,1,1,1,LPM_P_JPHIP),1,'ptw')
      endif

      return
      end
c-----------------------------------------------------------------------
      subroutine userbc (ix,iy,iz,iside,eg)
      include 'SIZE'
      include 'TSTEP'
      include 'NEKUSE'
      include 'INPUT'
      include 'GEOM' 

      ux = 0.0
      uy = 0.0
      uz = 0.0

      return
      end
c-----------------------------------------------------------------------
      subroutine useric (ix,iy,iz,eg)
      include 'SIZE'
      include 'TOTAL'
      include 'NEKUSE'
      integer e,eg, eqnum

      ux = 0.
      uy = 0.
      uz = 0.

      return
      end
c-----------------------------------------------------------------------
      subroutine usrdat
      include 'SIZE'
      include 'TOTAL'

      return
      end
!-----------------------------------------------------------------------
      subroutine usrdat2

      return
      end
c-----------------------------------------------------------------------
      subroutine usrdat3
      include 'SIZE'
      include 'TOTAL'
#     include "LPM"

      real rparam(lpm_nparam) 
      common /SCRNS/ yp(LPM_LPART*LPM_LRS),
     $               pp(LPM_LPART*LPM_LRP)

      rparam(1)  = 1           ! use custom values
      rparam(2)  = 1           ! time integration method
      rparam(3)  = 1           ! polynomial order of mesh
      rparam(4)  = 0           ! use 1 for tracers only
      rparam(5)  = LPM_R_JDP   ! index of filter non-dimensionalization in rprop
      rparam(6)  = 50.0        ! non-dimensional Gaussian filter width
      rparam(7)  = 1E-05       ! percent decay of Gaussian filter
      rparam(8)  = 1           ! periodic in x 
      rparam(9)  = 1           ! periodic in y
      rparam(10) = 1           ! periodic in z

      call init_particles(yp,pp,npart)
      call lpm_init      (rparam,yp,LPM_LRS,pp,LPM_LRP,npart,time)

      return
      end
!-----------------------------------------------------------------------
      subroutine init_particles(yp,pp,npart)
      include 'SIZE'
      include 'TOTAL'
#     include "LPM"

      real      yp(*)
      real      pp(*)

      npart   = 100     ! particles/rank to distribute
      dp      = 0.01    ! particle diameter
      rhop    = 2.0     ! particle density
      rdum    = ran2(-1-nid) ! initialize random number generator

      do i=1,npart
         ! set initial conditions for solution
         j = LPM_LRS*(i-1)
         yp(LPM_JX +j) = 2.0 + ran2(2)*2.0
         yp(LPM_JY +j) = 2.0 + ran2(2)*2.0
         yp(LPM_JZ +j) = 0.0
         yp(LPM_JVX+j) = 0.0
         yp(LPM_JVY+j) = 0.0
         yp(LPM_JVZ+j) = 0.0
      
         ! set initial particle properties
         jj = LPM_LRP*(i-1)
         pp(LPM_R_JRHOP+jj) = rhop
         pp(LPM_R_JDP  +jj) = dp
         pp(LPM_R_JVOLP+jj) = pi/6.0*pp(LPM_R_JDP+jj)**3
      enddo

      return
      end
c----------------------------------------------------------------------
      subroutine lpm_fun(time_,y,ydot)
      include 'SIZE'
      include 'TOTAL'
#     include "LPM"

      real time_
      real y(*)
      real ydot(*)

      COMMON /SCRNS/ wk1 (LX1,LY1,LZ1,LELV)
     >              ,wk2 (LX1,LY1,LZ1,LELV)
     >              ,pm1 (LX1,LY1,LZ1,LELV)
     >              ,dpdx(LX1,LY1,LZ1,LELV)
     >              ,dpdy(LX1,LY1,LZ1,LELV)
     >              ,dpdz(LX1,LY1,LZ1,LELV)

      if (ifsplit) then
         ntot = lx1*ly1*lz1*nelv
         call copy (pm1,pr,ntot)
      else
         call mappr(pm1,pr,wk1,wk2)
      endif
      call opgrad (dpdx,dpdy,dpdz,pm1)

      call lpm_interpolate_setup

      call lpm_interpolate_fld(LPM_R_JRHO , vtrans)
      call lpm_interpolate_fld(LPM_R_JUX  , vx    )
      call lpm_interpolate_fld(LPM_R_JUY  , vy    )
      call lpm_interpolate_fld(LPM_R_JUZ  , vz    )
      call lpm_interpolate_fld(LPM_R_JDPDX, dpdx  )
      call lpm_interpolate_fld(LPM_R_JDPDY, dpdy  )
      call lpm_interpolate_fld(LPM_R_JDPDZ, dpdz  )

      do i=1,lpm_npart
         ! striding solution y vector
         j = LPM_LRS*(i-1)

         ! fluid viscosity
         rmu   = param(2)

         ! particle mass
         rmass = lpm_rprop(LPM_R_JVOLP,i)*lpm_rprop(LPM_R_JRHOP,i)

         ! particle Reynolds number
         rvmag =   (lpm_rprop(LPM_R_JUX,i) - y(LPM_JVX+j))**2
     >           + (lpm_rprop(LPM_R_JUY,i) - y(LPM_JVY+j))**2
         if (if3d) rvmag = rvmag
     >           + (lpm_rprop(LPM_R_JUY,i) - y(LPM_JVY+j))**2
         rvmag = sqrt(rvmag)
         rep = rvmag*lpm_rprop(LPM_R_JRHO,i)*lpm_rprop(LPM_R_JDP,i)/rmu
         rep = max(1E-12,rep)

         ! Drag force (Schiller-Naumann drag)
         rdum  = 18.0*rmu/lpm_rprop(LPM_R_JDP,i)**2
         rdum  = rdum*(1.0 + 0.15*rep**0.687)
         rdum  = rdum*lpm_rprop(LPM_R_JVOLP,i)
         fqsx  = rdum*(lpm_rprop(LPM_R_JUX,i) - y(LPM_JVX+j))
         fqsy  = rdum*(lpm_rprop(LPM_R_JUY,i) - y(LPM_JVY+j))
         fqsz  = rdum*(lpm_rprop(LPM_R_JUZ,i) - y(LPM_JVZ+j))

         ! Gravity force
         fbx =  0.0
         fby = -9.8*rmass
         fbz =  0.0

         ! Pressure gradient force
         fpgx = -lpm_rprop(LPM_R_JVOLP,i)*lpm_rprop(LPM_R_JDPDX,i)
         fpgy = -lpm_rprop(LPM_R_JVOLP,i)*lpm_rprop(LPM_R_JDPDY,i)
         fpgz = -lpm_rprop(LPM_R_JVOLP,i)*lpm_rprop(LPM_R_JDPDZ,i)

         ! save particle-fluid coupling force
         lpm_ydotc(LPM_JVX,i) = fqsx
         lpm_ydotc(LPM_JVY,i) = fqsy
         lpm_ydotc(LPM_JVZ,i) = fqsz

         ! set ydot for all LPM_SLN number of equations
         ydot(LPM_JX +j) = y(LPM_JVX +j)
         ydot(LPM_JY +j) = y(LPM_JVY +j)
         if (if3d) ydot(LPM_JZ +j) = y(LPM_JVZ +j)
         ydot(LPM_JVX+j) = (fqsx+fbx+fpgx)/rmass
         ydot(LPM_JVY+j) = (fqsy+fby+fpgy)/rmass
         if (if3d) ydot(LPM_JVZ+j) = (fqsz+fbz+fpgz)/rmass
      enddo 

      call lpm_project

      return
      end
!-----------------------------------------------------------------------
      subroutine lpm_project_map(map,y,ydot,ydotc,rprop)
c
c     defines what Lagrangian particle data gets mapped into what 
c     Eulerian field index in lpm_pro_fld(:,:,:,:,index)
c     
      real map(*)
      real y(*)
      real ydot(*)
      real ydotc(*)
      real rprop(*)

      map(LPM_P_JPHIP) = rprop(LPM_R_JVOLP)   ! particle volume
      map(LPM_P_JFFX)  = -ydotc(LPM_JVX)      ! coupled force_x
      map(LPM_P_JFFY)  = -ydotc(LPM_JVY)      ! coupled force_y
      map(LPM_P_JFFZ)  = -ydotc(LPM_JVZ)      ! coupled force_z

      return
      end
!-----------------------------------------------------------------------
      FUNCTION ran2(idum)
      INTEGER idum,IM1,IM2,IMM1,IA1,IA2,IQ1,IQ2,IR1,IR2,NTAB,NDIV 
      REAL ran2,AM,EPS,RNMX
      PARAMETER (IM1=2147483563,IM2=2147483399,AM=1./IM1,IMM1=IM1-1,
     $        IA1=40014,IA2=40692,IQ1=53668,IQ2=52774,IR1=12211,
     $        IR2=3791,NTAB=32,NDIV=1+IMM1/NTAB,EPS=1.2e-7,RNMX=1.-EPS)
c Long period (> 2 ! 1018 ) random number generator of Lâ€™Ecuyer with 
c Bays-Durham shuffle and added safeguards. Returns a uniform random deviate 
c between 0.0 and 1.0 (exclusive of the endpoint values). 
c Call with idum a negative integer to initialize; thereafter, do not alter 
c idum between successive deviates in a sequence. RNMX should approximate the 
c largest floating value that is less than 1.
      INTEGER idum2,j,k,iv(NTAB),iy
      SAVE iv,iy,idum2
      DATA idum2/123456789/, iv/NTAB*0/, iy/0/
      if (idum.le.0) then 
         idum1=max(-idum,1) 
         idum2=idum1
         do j=NTAB+8,1,-1
            k=idum1/IQ1
            idum1=IA1*(idum1-k*IQ1)-k*IR1 
            if (idum1.lt.0) idum1=idum1+IM1 
            if (j.le.NTAB) iv(j)=idum1
         enddo
         iy=iv(1) 
      endif
      k=idum1/IQ1 
      idum1=IA1*(idum1-k*IQ1)-k*IR1
      if (idum1.lt.0) idum1=idum1+IM1 
      k=idum2/IQ2 
      idum2=IA2*(idum2-k*IQ2)-k*IR2 
      if (idum2.lt.0) idum2=idum2+IM2 
      j=1+iy/NDIV
      iy=iv(j)-idum2
      iv(j)=idum1 
      if(iy.lt.1)iy=iy+IMM1 
      ran2=min(AM*iy,RNMX)
      return
      END
c----------------------------------------------------------------------
